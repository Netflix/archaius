// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories { mavenCentral() }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { mavenCentral() }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
    dependencies {
        compile 'com.google.code.findbugs:annotations:2.0.0'
        testCompile('log4j:log4j:1.2.16')
    }
    group = "com.netflix.${githubProjectName}" 

    apply plugin: 'java'
    tasks.withType(Test) { forkEvery = 1 }
}

project(':archaius-core') {
    dependencies {
        compile 'commons-configuration:commons-configuration:1.8'
        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'com.google.guava:guava:11.0.2'    
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.11'    
        compile 'org.codehaus.jackson:jackson-core-asl:1.9.11'    
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.7.5'
        testCompile 'org.apache.derby:derby:10.8.2.2'        
    }
}

project(':archaius-aws') {
    dependencies {
        compile project(':archaius-core')
        compile 'com.amazonaws:aws-java-sdk:1.3.31'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-jclouds') {
    dependencies {
        compile project(':archaius-core')
        compile 'org.jclouds:jclouds-blobstore:1.5.3'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-zookeeper') {
    dependencies {
        compile project(':archaius-core')
        compile('org.apache.zookeeper:zookeeper:3.4.5') {
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'javax.jms', module: 'jms'
        }
        compile 'com.netflix.curator:curator-client:1.2.6'
        compile 'com.netflix.curator:curator-recipes:1.2.6'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
        testCompile 'com.netflix.curator:curator-test:1.2.6'
    }
}

project(':archaius-scala') {
    apply plugin: 'scala'

    dependencies {
        // Libraries needed to run the scala tools
        scalaTools 'org.scala-lang:scala-compiler:2.10.1'
        scalaTools 'org.scala-lang:scala-library:2.10.1'

        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.10.1'
        testCompile 'org.scalatest:scalatest_2.10.0:1.8'
        testCompile 'junit:junit:4.11'
    }
}

project(':archaius-samplelibrary') {
    apply plugin: 'scala'

    dependencies {
        scalaTools 'org.scala-lang:scala-compiler:2.10.1'
        scalaTools 'org.scala-lang:scala-library:2.10.1'

        compile project(':archaius-scala')

        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'org.scala-lang:scala-library:2.10.1'

        testCompile 'org.scalatest:scalatest_2.10.0:1.8'
        testCompile 'junit:junit:4.11'
    }
    jar {
        from('src/main/java') {
            include 'META-INF/conf/springbeans.xml'
            include 'META-INF/conf/config.properties'
        }
    }
}

