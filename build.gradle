plugins {
    id 'nebula.netflixoss' version '2.2.10'
    id 'nebula.provided-base' version '2.0.1'
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'nebula.provided-base'
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        jcenter()
    }
    dependencies {
        compile 'com.google.code.findbugs:jsr305:3.0.1'
        testCompile('log4j:log4j:1.2.16')
    }
    group = "com.netflix.${githubProjectName}" 

    tasks.withType(Test) { forkEvery = 1 }
    test {
        jvmArgs += [ "-XX:MaxPermSize=512m" ] // for archaius-scala
        maxHeapSize = '2g' // or however much memory the tests need
        testLogging {
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams  = true
        }

    }
}

project(':archaius-core') {
    dependencies {
        compile 'commons-configuration:commons-configuration:1.8'
        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'com.google.guava:guava:16.0'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.3'
        compile 'com.fasterxml.jackson.core:jackson-core:2.4.3'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.4.3'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.7.5'
        testCompile 'org.apache.derby:derby:10.8.2.2'
        testCompile 'org.apache.commons:commons-io:1.3.2'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile files('src/test/resources/classpathTestResources.jar')

    }
}

project(':archaius-aws') {
    dependencies {
        compile project(':archaius-core')
        compile 'com.amazonaws:aws-java-sdk-core:1.9.3'
        compile 'com.amazonaws:aws-java-sdk-dynamodb:1.9.3'
        compile 'com.amazonaws:aws-java-sdk-s3:1.9.3'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

//project(':archaius-jclouds') {
//    dependencies {
//        compile project(':archaius-core')
//        compile 'org.jclouds:jclouds-blobstore:1.6.0'
//        testCompile 'junit:junit:4.11'
//        testCompile 'org.slf4j:slf4j-simple:1.6.4'
//    }
//}

project(':archaius-typesafe') {
    dependencies {
        compile project(':archaius-core')
        compile 'com.typesafe:config:1.2.1'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-zookeeper') {
    dependencies {
        compile project(':archaius-core')
        compile('org.apache.zookeeper:zookeeper:3.4.5') {
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'javax.jms', module: 'jms'
        }
        compile 'org.apache.curator:curator-client:2.3.0'
        compile 'org.apache.curator:curator-recipes:2.3.0'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
        testCompile 'org.apache.curator:curator-test:2.3.0'
    }
}

project(':archaius-etcd') {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(':archaius-core')
        compile 'com.google.guava:guava:19.0'
        compile 'io.fastjson:etcd-client:0.33'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-scala') {
    apply plugin: 'scala'

    dependencies {
        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.10.4'
        testCompile 'org.scalatest:scalatest_2.10:2.2.5'
        testCompile 'junit:junit:4.11'
    }
}

project(':archaius-samplelibrary') {
    apply plugin: 'scala'

    dependencies {
        compile project(':archaius-scala')

        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'org.scala-lang:scala-library:2.10.1'

        testCompile 'org.scalatest:scalatest_2.10.0:1.8'
        testCompile 'junit:junit:4.11'
    }
    jar {
        from('src/main/java') {
            include 'META-INF/conf/springbeans.xml'
            include 'META-INF/conf/config.properties'
        }
    }
}
